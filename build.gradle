import io.github.fvarrui.javapackager.gradle.PackageTask
import io.github.fvarrui.javapackager.model.Platform

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'io.github.fvarrui:javapackager:1.5.0'
    }
}

plugins {
    id 'java'
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id "org.beryx.jlink" version "2.16.4"
}

apply plugin: 'io.github.fvarrui.javapackager.plugin'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.12.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.2'
    testImplementation 'junit:junit:4.13'
}

version = '0.0.1'
mainClassName = 'gyromite/gyromite.Main'

application {
    applicationDefaultJvmArgs = ['-Dsun.java2d.uiScale=1']
}

javafx {
    version = "15.0.1"
    modules = [ 'javafx.media' ]
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

javadoc {
    options.encoding = 'UTF-8'
}

task packageApp(type: PackageTask, dependsOn: build) {
    mainClass = 'gyromite.Main'
    vmArgs = ['-Dsun.java2d.uiScale=1']
    platform = Platform.auto
    bundleJre = true
    generateInstaller = false
    createZipball = true
    createTarball = true
}

